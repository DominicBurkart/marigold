WHITESPACE = _{ " " }

simple_complexity = { SOI ~ "O(" ~ base_complexity ~ ")" ~ EOI }

exact_complexity = { SOI ~ "O(" ~ exact_expr ~ ")" ~ EOI }
exact_expr = { exact_term ~ ("+" ~ exact_term)* }
exact_term = { coefficient ~ base_complexity_non_constant | constant_term | base_complexity }
coefficient = @{ ASCII_DIGIT+ }
constant_term = @{ ASCII_DIGIT+ }

base_complexity = { "1" | base_complexity_non_constant }
base_complexity_non_constant = {
    "log(n)"
  | "n" ~ "!/" ~ "(n-" ~ number ~ ")!"
  | "n" ~ "!"
  | "n" ~ "^" ~ number
  | "n" ~ "*log(n)"
  | "n" ~ "*log(" ~ number ~ ")"
  | "n"
  | "C(n," ~ number ~ ")"
  | "?"
}

number = @{ ASCII_DIGIT+ }

symbolic = { SOI ~ symbolic_expr ~ EOI }
symbolic_expr = { symbolic_sum | symbolic_atom }
symbolic_sum = { symbolic_atom ~ ("+" ~ symbolic_atom)+ }
symbolic_atom = { symbolic_filtered | symbolic_min | symbolic_perm | symbolic_perm_rep | symbolic_comb | symbolic_constant | symbolic_unknown }
symbolic_filtered = { "\u{2264}" ~ symbolic_atom }
symbolic_min = { "min(" ~ symbolic_expr ~ ", " ~ symbolic_expr ~ ")" }
symbolic_perm = { "P(" ~ symbolic_expr ~ ", " ~ number ~ ")" }
symbolic_perm_rep = { symbolic_constant ~ "^" ~ number }
symbolic_comb = { "C(" ~ symbolic_expr ~ ", " ~ number ~ ")" }
symbolic_constant = @{ ASCII_DIGIT+ }
symbolic_unknown = { "?" }
