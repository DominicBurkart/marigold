use crate::nodes;

grammar;

pub Expr: String = {
  Stream
  // next up: Declaration
}

Stream: String = {
  <inp: InputFunction> <funs:("." <StreamFunction>)*> "." <out: OutputFunction> => {
      let exp = nodes::StreamNode {
        inp,
        funs,
        out
      };
      exp.code()
    },
}

InputFunction: nodes::InputFunctionNode = {
  "range(" <n1: r"[0-9]+"> "," <n2: r"[0-9]+"> ")" => nodes::InputFunctionNode{
    variability: nodes::InputVariability::Constant,
    input_count: nodes::InputCount::Known((n2.parse::<num_bigint::BigInt>().unwrap() - n1.parse::<num_bigint::BigInt>().unwrap()).to_biguint().unwrap()),
    code: format!("::marigold::marigold_impl::futures::stream::iter({n1}..{n2})"),
  },
  "read_file(" <path: r#""[0-9A-Za-z/._\-]+""#> "," "csv)" => nodes::InputFunctionNode{
    variability: nodes::InputVariability::Variable,
    input_count: nodes::InputCount::Unknown,
    code: format!("::marigold::marigold_impl::csv_async::AsyncReader::from_reader(
      ::marigold::marigold_impl::async_std::fs::File::open({path})
        .await
        .expect(\"Marigold could not open file\")
    ).into_records()")
  }
}

StreamFunction: nodes::StreamFunctionNode = {
  "permutations("<n: r"[0-9]+"> ")" => nodes::StreamFunctionNode {
    code: format!("permutations({n}).await")
  },
  "permutations_with_replacement("<n: r"[0-9]+"> ")" => nodes::StreamFunctionNode {
    code: format!("collect_and_apply(|values| async {{
              ::marigold::marigold_impl::gen_nested_iter_yield::nested_iter_yield!(values.iter(), {n}, .to_owned(), ::marigold::marigold_impl::)
          }})
          .await
          .await
    ")
  },
  "combinations("<n: r"[0-9]+"> ")" =>  nodes::StreamFunctionNode {
    code: format!("combinations({n}).await")
  },
  "keep_first_n(" <n: r"[0-9]+"> "," <value_fn: r"[a-zA-Z_]+"> ")" =>  nodes::StreamFunctionNode {
    code: format!("keep_first_n({n}, {value_fn}).await")
  },
  "filter(" <filter_fn: r"[a-zA-Z_]+"> ")" => nodes::StreamFunctionNode {
    code: format!("filter(|v| futures::future::ready({filter_fn}(v)))")
    // filters have a bad type that doesn't allow them to compile if passed
    // an actual async function, so wrap a sync function in a fake future until
    // the filter types are updated.
  },
  "filter_map(" <filter_map_fn: r"[a-zA-Z_]+"> ")" => nodes::StreamFunctionNode {
    code: format!("filter_map({filter_map_fn})")
  },
  "to_vec()" =>  nodes::StreamFunctionNode {
    code: format!("to_vec().await")
  },
  "map(" <mapping_fn: r"[a-zA-Z_]+"> ")" => nodes::StreamFunctionNode {
    code: format!("map({mapping_fn})")
  }
}

OutputFunction: nodes::OutputFunctionNode = {
  "return" => nodes::OutputFunctionNode {
    stream_prefix: "return".to_string(),
    stream_postfix: ";".to_string(),
  },
  "write_file(" <path: r#""[0-9A-Za-z/._\-]+""#> "," "csv)" => nodes::OutputFunctionNode {
    stream_prefix: format!("{{
      let output_csv_writer = ::marigold::marigold_impl::csv_async::AsyncWriter::from_writer(
    ::marigold::marigold_impl::async_std::fs::OpenOptions::new()
       .write(true)
       .open(\"{path}\")
       .await
       .expect(\"Could not write to file\"));
     "),
    stream_postfix: ".for_each(|v| output_csv_writer.write_record(v).await.expect(\"could not write record to CSV\")); }".to_string(),
  }
}
